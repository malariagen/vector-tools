import os
import numpy as np
import pandas as pd
import re

# read_fofn
fofn_path = config["fofn"]
sampleset = os.path.basename(os.path.dirname(fofn_path))

fofn = pd.read_table(fofn_path)
samples = fofn['sample'].unique()

# create a table of paths
path_df = pd.DataFrame(index=samples)
path_df["path"] = [os.path.join(config["input_location"], x + ".genotypes.zarr.zip") for x in samples]

# HACK NOT IN PRODUCTION>>>
path_df["exists"] = path_df.path.map(os.path.isfile)
samples = path_df.loc[path_df.exists].index.tolist()

rule qc_report:
  input:
    os.path.join(sampleset, "qc_summary.tsv")


rule generate_report:
  input:
    manifest="{sampleset}/manifest",
    csv=expand("{{sampleset}}/{sample}.callstats.csv", sample=samples),
    npy=expand("{{sampleset}}/{sample}.covhist.npz", sample=samples),
    contam=expand("{{sampleset}}/{sample}.contamination.csv", sample=samples),
    cfg="{sampleset}/qc_config.yml",
    fofn=fofn_path
  params:
    min_fgc=config["min_fgc"],
    min_med_cov=config["min_med_cov"],
    min_male_xratio=config["min_male_xratio"],
    max_male_xratio=config["max_male_xratio"],
    min_female_xratio=config["min_female_xratio"],
    max_female_xratio=config["max_female_xratio"],
    max_pc_contam=config["max_pc_contam"],
    path=lambda y: os.path.join(y.sampleset, "{sample}"),
    req="h_vmem=6G"
  output:
    csv="{sampleset}/qc_summary.tsv"
  script:
    "../../scripts/combine_qc_cohort_stats.py"


rule write_config:
  output:
    cfg="{sampleset}/qc_config.yml"
  params:
    req="h_vmem=2G"
  run:
    import yaml
    with open(output.cfg, "w") as fh:
      yaml.dump(config, fh)


rule estimate_contamination:
  input:
    input=lambda y: path_df.path.loc[y.sample],
    sites=config["genotyped_sites"],
    allele_frequencies=config["allele_frequencies"]
  output:
    txt="{sampleset}/{sample}.contamination.csv"
  params:
    stem="{sampleset}/{sample}",
    seqid=config["seqid_contam"],
    downsample=config["downsample"],
    minimum_af=config["minimum_af"],
    minimum_coverage=config["minimum_coverage"],
    seq_err_rate=config["ser"],
    plot=True,
    log=False,
    req="h_vmem=12G"
  script: 
    "../../scripts/estimate_contamination.py"


rule coverage_summary:
  input:
    input=lambda y: path_df.path.loc[y.sample],
  output:
    csv="{sampleset}/{sample}.callstats.csv",
    npy="{sampleset}/{sample}.covhist.npz"
  params:
    stem="{sampleset}/{sample}",
    seqid=config["seqid_stats"],
    req="h_vmem=12G"
  script: 
    "../../scripts/calculate_alignment_summary_stats.py"


# required for qc_report.
rule make_manifest:
  output:
    manifest="{sampleset}/manifest"
  params:
    req="h_vmem=2G"
  run:
    with open(output.manifest, "w") as fh:
      print("\n".join(samples), file=fh)

